plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'com.jfrog.artifactory' version '4.13.0'
    id 'maven-publish'
    id 'signing'
}
apply from: "../common-methods.gradle"

android {

    compileSdkVersion COMPILE_SDK_VERSION
    useLibrary 'org.apache.http.legacy'

    defaultConfig {
        minSdkVersion MIN_SDK_VERSION
        consumerProguardFiles CONSUMER_PROGUARD_FILES

        def packageIdentifier =  System.getenv('LEANPLUM_PACKAGE_IDENTIFIER') ?: 's'
        buildConfigField 'String', 'LEANPLUM_PACKAGE_IDENTIFIER', "\"${packageIdentifier}\""

        buildConfigField 'String', 'SDK_VERSION', "\"${LEANPLUM_SDK_VERSION}\""

        def buildNumber = System.getenv('BUILD_NUMBER') ?: '0'
        buildConfigField 'String', 'BUILD_NUMBER', "\"${buildNumber}\""
    }

    buildTypes {
        release {
            proguardFiles getDefaultProguardFile('proguard-android.txt'), PROGUARD_FILES
        }
    }

    compileOptions {
        sourceCompatibility JAVA_VERSION
        targetCompatibility JAVA_VERSION
    }
}

dependencies {
    // Compile dependencies will be added as dependency in pom file.
    api "androidx.legacy:legacy-support-v4:1.0.0"
    api "androidx.appcompat:appcompat:${APPCOMPAT_LIBRARY_VERSION}"

    api fileTree(include: ['*.jar', '*.aar'], dir: 'libs')
    api files('libs/clevertap-android-sdk-4.5.0.aar')    // clevertap core sdk
    api files('libs/clevertap-geofence-sdk-1.1.0.aar')    // for geofencing
    api files('libs/clevertap-xiaomi-sdk-1.4.0.aar')    // for xiaomi push.
    api files('libs/push-templates-1.0.2.aar')    // for Push Templates

}

task generateJavadoc(type: Javadoc) {
    exclude 'com/leanplum/internal'
    exclude 'com/leanplum/messagetemplates'
    exclude 'com/leanplum/utils'
    exclude 'com/leanplum/views'
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    destinationDir = file("./javadoc/")
    failOnError false
}

publishing_task(LEANPLUM_CORE_ARTIFACT_ID)
